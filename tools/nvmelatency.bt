#!/usr/bin/env bpftrace

#include<linux/blkdev.h>
#include<linux/nvme.h>

BEGIN
{

	printf("Tracing nvme command latency.Hit Ctrl-C to end.\n");
	// from 1inux/nvme h:
	@ioopcode[0] = "nvme_cmd_read";
	@ioopcode[1] = "nvme_cmd_write";
	@ioopcode[2] = "nvme_cmd_flush";
	@ioopcode[13] = "nvme_cmd_zone_append";
}

kprobe:smi_nvme_setup_cmd
,kprobe:nvme_setup_cmd
{
	$req = (struct request*)arg1;
	if($req->rq_disk != 0) {
		@start[arg1] = nsecs;
		$flag = ($req->cmd_flags & ((1 << 8) - 1));
		if ($flag <= 2 ||$flag == 13) {
			@cmd_flags[arg1] = $flag;
		}
		
	} else {
		@admin_commands = count();
	}
}
kprobe:smi_nvme_complete_rq
,kprobe:nvme_complete_rq
/@start[arg0]/
{
	$req = (struct request*)arg0;
	$disk = $req->rq_disk;
	$flag = ($req->cmd_flags & ((1 << 8) - 1));
	if ($flag <= 2 ||$flag == 13) {
		$duration = (nsecs - @start[arg0]) / 1000;
		@usecs[$disk->disk_name,@ioopcode[$flag]] = hist($duration);
	}
	
	delete(@start[arg0]);
	delete(@cmd_flags[arg0]);
}

interval:s:1
{
	time("======= %Y-%m-%d %H:%M:%S  ======================\n");
	print(@usecs);
	clear(@usecs);
}

END
{
	clear(@ioopcode);
	clear(@start);
	clear(@cmd_flags);
	clear(@usecs);
}
